apply plugin: 'application'

dependencies {
    compile group: 'com.github.kklongming', name: 'sz-ebean', version: 'unspecified'
    compile group: 'com.github.kklongming', name: 'sz-api-doc', version: 'unspecified'
    compile group: 'com.github.kklongming', name: 'sz-plan-task', version: 'unspecified'

    // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.58'

    compile "org.jtwig:jtwig-core:5.87.0.RELEASE"

    compile project(':common_lib')
    compile project(':models')
    compile project(':sz-tushare')
    compile project(':sz-futu-protocol')
    compile project(':sz-futu-client')
    compile files("conf")
}

distZip {
    into(project.name) {
        from '.'
        include 'conf/**'
    }
}

distTar {
    into(project.name) {
        from '.'
        include 'conf/**'
    }
}

mainClassName = 'sz.api.server.ApiServer'
// 可以在此添加jvm内存参数, eg: '-Xms512m', '-Xmx4096m'
applicationDefaultJvmArgs = ['-Duser.timezone=GMT+8', '-Dfile.encoding=UTF-8', '-Dsun.jnu.encoding=UTF-8']

tasks.withType(CreateStartScripts) {
    it.unixStartScriptGenerator.template = resources.text.fromFile('unixStartScript.txt')
}

tasks.withType(JavaExec) {
    if (project.hasProperty("debug")) {
        def debugPort = "5005"
        if (project.findProperty("debug").toString().isNumber()) {
            debugPort = project.findProperty("debug").toString()
        }
        it.jvmArgs.add("-Xdebug")
        it.jvmArgs.add("-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=${debugPort}")
        println("\u001b[35m" + "Remote debug is enabled. debug port: ${debugPort}" + "\u001b[0m")
    }

    it.systemProperties(System.properties as Map<String, ?>)
}
